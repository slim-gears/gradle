import groovy.text.SimpleTemplateEngine

project.ext {
    sourceCompatibility = 1.8
    props = [:]
}

static def loadProperties(Project prj, File file) {
    def props = new Properties()
    file.withInputStream { props.load(it) }

    def evaluator = new SimpleTemplateEngine()
    Map bindings = new HashMap(props)
    bindings.projectName = prj.name
    def name = file.name.take(file.name.lastIndexOf('.')) as String
    def needsEvaluation = true
    while (needsEvaluation) {
        def evaluatedCount = 0
        new HashMap(bindings)
                .findAll { (it.value as String).contains('$') }
                .forEach { key, value ->
            def template = evaluator.createTemplate(value.toString())
            def evaluated = template.make(bindings).toString()
            if (evaluated != value) {
                evaluatedCount++
                bindings[key] = evaluated
            }
        }
        needsEvaluation = evaluatedCount > 0
    }
    prj.ext[name] = bindings
    prj.ext.props[name] = prj.ext[name]
}

allprojects { Project prj ->
    group rootProject.group
    version rootProject.version

    prj.projectDir
            .listFiles ({ dir, name -> name ==~ /.*\.properties/ } as FilenameFilter)
            .each { loadProperties(prj, it) }
}

subprojects {
    repositories {
        jcenter()
        mavenLocal()
        maven { url 'https://dl.bintray.com/slim-gears/maven' }
        maven { url 'https://jitpack.io' }
    }
}
