apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'net.ltgt.apt-idea'

sourceCompatibility = rootProject.sourceCompatibility

dependencies {
    testImplementation libs.junit
    testImplementation libs.mockito
}

configurations {
    bundledImplementation
}

project.afterEvaluate {
    configurations {
        implementation.extendsFrom(bundledImplementation)
        testImplementation.extendsFrom(bundledImplementation)
    }

    configurations.each { configuration ->
        ProjectDependency[] prjDeps = configuration.dependencies
                .findAll { it instanceof ProjectDependency }
                .collect { it as ProjectDependency }
                .findAll { it.targetConfiguration == null }
        prjDeps.each { dep ->
            project.dependencies {
                it.add(configuration.name, it.project(path: dep.dependencyProject.path, configuration: 'bundledImplementation'))
            }
        }
    }

    project.jar {
        from {
            configurations.bundledImplementation.collect { it.isDirectory() ? it : zipTree(it) }
        }
    }
}
